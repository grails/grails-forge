plugins {
    id "io.github.gradle-nexus.publish-plugin"
    id "org.grails.forge.internal.aggregator"
}

version project.projectVersion
group "org.grails.forge"

def ossUser = System.getenv("SONATYPE_USERNAME") ?: rootProject.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
def ossPass = System.getenv("SONATYPE_PASSWORD") ?: rootProject.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
def ossStagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: project.hasProperty("sonatypeOssStagingProfileId") ? project.sonatypeOssStagingProfileId : ''

if (!ossUser.isEmpty() && !ossPass.isEmpty()) {
    String version = String.valueOf(rootProject.getVersion());
    if ("unspecified".equals(version)) {
        throw new RuntimeException("The root project doesn't define a version. Please set the version in the root project.");
    }
    nexusPublishing {
        repositories {
            sonatype {
                allowInsecureProtocol = rootProject.hasProperty("allowInsecurePublishing")
                username = ossUser
                password = ossPass
                stagingProfileId = ossStagingProfileId
                nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
                snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            }
        }
    }
}