plugins {
    id("com.github.johnrengelman.shadow")
    id("io.micronaut.application") version "3.7.10"
}

version project.projectVersion
group "org.grails.forge"

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(":grails-forge-api"))
    implementation("io.micronaut.gcp:micronaut-gcp-http-client")
    runtimeOnly("ch.qos.logback:logback-classic")
    testCompileOnly("io.micronaut:micronaut-inject-groovy:$micronautVersion")
    testImplementation("io.micronaut.test:micronaut-test-spock")
    testImplementation("io.micronaut:micronaut-http-client")
}
tasks.named("test", Test) {
    useJUnitPlatform()
    classpath += configurations.developmentOnly
}
application {
    mainClass = "org.grails.forge.netty.Application"
}
tasks.named("run") {
    classpath += configurations.developmentOnly
}
graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("org.grails.forge.*")
    }
}
tasks.named('dockerfileNative') {
    // https://www.graalvm.org/latest/docs/getting-started/container-images/
    baseImage("ghcr.io/graalvm/jdk:ol8-java17-22.3.2")
}

tasks.named("dockerBuildNative") {
    images = [project.hasProperty("dockerImageName") ? project.getProperty("dockerImageName") : "grailsforge"]
}

tasks.named("dockerBuild") {
    images = [project.hasProperty("dockerImageName") ? project.getProperty("dockerImageName") : "grailsforge"]
}