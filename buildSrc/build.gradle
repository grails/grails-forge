plugins {
    id "java-gradle-plugin"
    id "groovy-gradle-plugin"
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == 'org.codehaus.groovy') {
                details.useVersion '3.0.9'
                details.because 'Groovy 2.5.x not supported by Gradle 7.0+'
            }
            if (details.requested.group.startsWith("com.fasterxml.jackson")) {
                details.useVersion("2.15.0-rc1")
            }
        }
    }
}

dependencies {
    implementation "com.fizzed:rocker-compiler:1.3.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.15.0-rc1"
    implementation "io.sdkman:gradle-sdkvendor-plugin:3.0.0"
    implementation "org.asciidoctor:asciidoctor-gradle-jvm:3.3.2"
    implementation "com.diffplug.spotless:spotless-plugin-gradle:6.15.0"
    implementation "io.spring.nohttp:nohttp-gradle:0.0.11"
    implementation "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
    implementation "io.github.groovylang.groovydoc:io.github.groovylang.groovydoc.gradle.plugin:1.0.1"
    implementation "io.github.gradle-nexus:publish-plugin:1.3.0"
}

gradlePlugin {
    plugins {
        rockerPlugin {
            id = "org.grails.forge.rocker"
            implementationClass = "org.grails.forge.rocker.plugin.RockerPlugin"
        }
    }
}
