import org.grails.forge.internal.tasks.WriteGrailsVersionInfoTask

plugins {
    id "org.grails.forge.internal.published-module"
    id "org.grails.forge.internal.convention"
    id "org.grails.forge.rocker"
}

sourceSets {
    main {
        rocker {
            srcDir("src/main/java")
        }
    }
}

configurations.all {
    it.resolutionStrategy {
        it.eachDependency { details ->
            if (details.requested.group.startsWith("com.fasterxml.jackson")) {
                details.useVersion("2.15.0-rc1")
            }
        }
    }
}

dependencies {
    api platform("io.micronaut:micronaut-bom:$micronautVersion")
    api "com.fizzed:rocker-runtime:$rockerVersion"
    api "io.micronaut:micronaut-inject"
    api "io.micronaut:micronaut-http"
    api "io.micronaut:micronaut-http-client"
    api "io.github.java-diff-utils:java-diff-utils:4.12"
    api "org.eclipse.jgit:org.eclipse.jgit:6.5.0.202303070854-r"
    implementation "org.yaml:snakeyaml:2.0"
    implementation 'com.typesafe:config:1.4.2'
    implementation 'org.apache.commons:commons-compress:1.23.0'
    compileOnly "com.google.code.findbugs:jsr305"
    testImplementation "org.codehaus.groovy:groovy-yaml"
}


def grailsVersionInfo = tasks.register("grailsVersionInfo", WriteGrailsVersionInfoTask) {
    version = grailsVersion
    outputDirectory = layout.buildDirectory.dir("version-info")
}

sourceSets.main.resources.srcDir(grailsVersionInfo)

rocker {
    javaVersion = '11'
    postProcessing = ['org.grails.forge.rocker.plugin.WhitespaceProcessor']
}

nohttp {
    source.exclude "**/pom.rocker.raw"
}

javadoc {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}