plugins {
    id "org.grails.forge.internal.build.cli-module"
}

sourceSets {
    main {
        rocker {
            srcDir("src/main/java")
        }
    }
}

ext {
    picocliVersion = '4.7.1'
}


configurations {
    generateConfig
}

dependencies {
    annotationProcessor("io.micronaut:micronaut-inject-java")
    api project(":grails-forge-core")
    api "info.picocli:picocli:${picocliVersion}"
    api "info.picocli:picocli-shell-jline3:${picocliVersion}"
    api "com.fizzed:rocker-runtime:$rockerVersion"
    implementation "org.slf4j:slf4j-nop:2.0.6"
    implementation "org.fusesource.jansi:jansi:2.4.0"
    implementation "org.yaml:snakeyaml:1.33"
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'
    implementation 'org.shredzone.acme4j:acme4j-client:2.16'
    implementation 'org.shredzone.acme4j:acme4j-utils:2.16'
    generateConfig "info.picocli:picocli-codegen:${picocliVersion}"
    compileOnly "com.google.code.findbugs:jsr305"
    testImplementation("io.micronaut.picocli:micronaut-picocli")
    testImplementation "org.testcontainers:spock"
    testImplementation 'org.reflections:reflections:0.10.2'
}

application {
    mainClass = "org.grails.forge.cli.Application"
}

rocker {
    javaVersion = '11'
    postProcessing = ['org.grails.forge.rocker.plugin.WhitespaceProcessor']
}

startScripts {
    applicationName = 'grails'
}

sdkman {
    api = "https://vendors.sdkman.io"
    consumerKey = System.getenv("GVM_SDKVENDOR_KEY") ?: project.hasProperty("gvmSdkvendorKey") ? project.gvmSdkvendorKey : ''
    consumerToken = System.getenv("GVM_SDKVENDOR_TOKEN") ?: project.hasProperty("gvmSdkvendorToken") ? project.gvmSdkvendorToken : ''
    candidate = "grails"
    version = project.version
    hashtag = "#grailsfw"
    platforms = [
            // TODO:  Once graal native-image works for arm OSX, we should switch to building and publishing a non-rosetta release (https://github.com/oracle/graal/issues/2666)
            "MAC_ARM64":"https://github.com/grails/grails-forge/releases/download/v${project.version}/grails-darwin-amd64-v${project.version}.zip",
            "MAC_OSX":"https://github.com/grails/grails-forge/releases/download/v${project.version}/grails-darwin-amd64-v${project.version}.zip",
            "WINDOWS_64":"https://github.com/grails/grails-forge/releases/download/v${project.version}/grails-win-amd64-v${project.version}.zip",
            "LINUX_64":"https://github.com/grails/grails-forge/releases/download/v${project.version}/grails-linux-amd64-v${project.version}.zip"
    ]
}