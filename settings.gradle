pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}
buildscript {
    dependencies {
        classpath 'org.antlr:antlr4-runtime:4.8-1!!'
    }
}
plugins {
    id "com.gradle.enterprise" version "3.17.6"
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.0.1'
}

gradleEnterprise {
    server = 'https://ge.grails.org'
    buildScan {
        publishAlways()
        publishIfAuthenticated()
        uploadInBackground = System.getenv("CI") == null
        capture {
            taskInputFiles = true
        }
    }
}

buildCache {
    local { enabled = System.getenv('CI') != 'true' }
    remote(gradleEnterprise.buildCache) {
        def isAuthenticated = System.getenv('GRADLE_ENTERPRISE_ACCESS_KEY')
        push = System.getenv('CI') == 'true' && isAuthenticated
        enabled = true
    }}


rootProject.name = 'grails-forge'

include 'grails-forge-core'
include 'grails-cli'
include 'test-core'
include 'grails-forge-api'
include 'grails-forge-web-netty'
include 'grails-forge-analytics-postgres'